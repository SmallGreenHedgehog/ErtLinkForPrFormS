Функция ВыборФайла(
					Путь
					,ФайлИлиКаталог=0 //0 - файл, 1 - каталог
					,РежимВыбора=0 //0 - открыть, 1 - сохранить, игнорируется для каталога
					,РаботаСОтносПутями=0
					,ПутьККорнКатал="" //игнорируется, если работа с относ. отключена
					,ТекстВыбора="По умолчанию"
					,Расширение="*"
					,ИмяФайлаПоУмолч=""	//игнорируется для каталога
					,ПроверитьСуществование=0
					,МинРазмерФайла=0
					,МаксРазмерФайла=0
					,ИмяРеквНаФорме=""
					,Таймаут=30
					)
	Перем Кат, Фай;
	Перем Результат;
	Перем ВыбПуть;	
	Перем FSO;
	Перем УдовлетворяетДопТреб;
	Перем  ТекРазмерФайла;
	
	Кат="";
	Фай="";   
	ВыбПуть="";
	FSO = СоздатьОбъект("Scripting.FileSystemObject");
	ОбъектФС="";
	
	Если ФайлИлиКаталог=0 Тогда
		Если ТекстВыбора="По умолчанию" Тогда
			ТекстВыбора="Выберите файл:"
		КонецЕсли;
		
		Если ИмяФайлаПоУмолч="" Тогда
			ИмяФайлаПоУмолч="def"+?(Расширение<>"*","."+Расширение,".txt");
		КонецЕсли;	
		
		Фай=ИмяФайлаПоУмолч;
		
		Результат=ФС.ВыбратьФайл(РежимВыбора,Фай,Кат,ТекстВыбора,"(*."+Расширение+")|*."+Расширение,,Таймаут);
		
		Если Результат=1 Тогда
			ВыбПуть=Кат+?(ПустаяСтрока(СокрЛП(Фай))=1, ИмяФайлаПоУмолч, Фай);
			Если ФС.СуществуетФайл(ВыбПуть)=1 Тогда
				ОбъектФС = FSO.GetFile(ВыбПуть);
			КонецЕсли;			
		КонецЕсли;		
	Иначе
		Если ТекстВыбора="По умолчанию" Тогда
			ТекстВыбора="Выберите каталог:"
		КонецЕсли;
		Результат=Фс.ВыбратьКаталог(ВыбПуть,ТекстВыбора,30);	
		Если Результат=1 Тогда
			ОбъектФС = FSO.GetFolder(ВыбПуть);
		КонецЕсли;		
	КонецЕсли;

	Если Результат=1 Тогда		
		УдовлетворяетДопТреб=1;			
				
		Если ПроверитьСуществование=1 Тогда		
			Если ФС.СуществуетФайл(ВыбПуть)=0 Тогда
				УдовлетворяетДопТреб=0;
				Предупреждение("Ошибка! Указанный объект не найден.",30);				
			КонецЕсли;			
		КонецЕсли;
		Если УдовлетворяетДопТреб=1 Тогда			
			Если (МинРазмерФайла>0) или (МаксРазмерФайла>0) Тогда
				Если ПустоеЗначение(ОбъектФС)=0 Тогда
					ТекРазмерФайла = Окр(ОбъектФС.Size/1024,0,1);
					
					Если (ТекРазмерФайла<МинРазмерФайла) или (ТекРазмерФайла>МаксРазмерФайла)  Тогда					
						УдовлетворяетДопТреб=0;
						Предупреждение("Ошибка! Указанный объект имеет размер не соответствующий предъявленным требованиям 
						|("+СокрЛП(МинРазмерФайла)+" КБ >= Файл ("+СокрЛП(ТекРазмерФайла)+" КБ) >="+СокрЛП(МаксРазмерФайла)+" КБ)");
					КонецЕсли;
				Иначе
					УдовлетворяетДопТреб=0;
					Предупреждение("Ошибка! Указанный объект не обнаружен. Проверка размера не возможна.");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;		
		
		Если УдовлетворяетДопТреб=1 Тогда
			Путь=ВыбПуть;
			//Путь=?(РаботаСОтносПутями=1,ПреобразованиеОтноситПутей(ВыбПуть, ПутьККорнКатал),ВыбПуть);
			
			Если ПустаяСтрока(ИмяРеквНаФорме)=0 Тогда
				Шаблон("[Форма."+ИмяРеквНаФорме+".Заголовок("""+Путь+""")]");
				Форма.Обновить();
			КонецЕсли;
		Иначе
			Результат=0;
		КонецЕсли;
	КонецЕсли;		
	FSO="";
	
	Возврат Результат;
КонецФункции // ВыборФайла

Функция ПолучитьПутьКОбработке()
	Перем ПутьКЯрлыку, ИмяЯрлыка, ПутьККонфигуЯрлыка;	
	Перем ПутьКОбработке;
	Перем Результат;
	
	Результат="";	
	
	РасположениеФайла(ПутьКЯрлыку, ИмяЯрлыка);	
	ПутьККонфигуЯрлыка=ПутьКЯрлыку+Лев(ИмяЯрлыка,СтрДлина(ИмяЯрлыка)-4)+".cfg";
	
	Если ФС.СуществуетФайл(ПутьККонфигуЯрлыка)=1 Тогда
		Текст = СоздатьОбъект("Текст");
		
		Текст.Открыть(ПутьККонфигуЯрлыка);
		ПутьКОбработке=Текст.ПолучитьСтроку(1);
		Если ФС.СуществуетФайл(ПутьКОбработке)=1 Тогда
			Результат=ПутьКОбработке;			
		КонецЕсли;
		
		Текст="";
	КонецЕсли;
	
	Возврат Результат;
КонецФункции // ПолучитьПутьКОбработке

Процедура УказатьПутьКОбработке()
	Перем ПутьКЯрлыку, ИмяЯрлыка, ПутьККонфигуЯрлыка;	
	Перем ПутьКОбработке;
	Перем Текст;
	
	РасположениеФайла(ПутьКЯрлыку, ИмяЯрлыка);	
	ПутьККонфигуЯрлыка=ПутьКЯрлыку+Лев(ИмяЯрлыка,СтрДлина(ИмяЯрлыка)-4)+".cfg";
	
	Если ВыборФайла(ПутьКОбработке,,,,,"Укажите файл, на который будет ссылаться ярлык:","ert",,1)=1 Тогда
		Текст=СоздатьОбъект("Текст");

		Текст.ВставитьСтроку(1, ПутьКОбработке);
		Текст.Записать(ПутьККонфигуЯрлыка);
		
		Текст="";		
	КонецЕсли;
КонецПроцедуры // УказатьПутьКОбработке

Процедура ПриОткрытии()
	Перем ПутьКОбработке;
	Перем КонтФормы;	
	
	СтатусВозврата(0);
	
	ПутьКОбработке=ПолучитьПутьКОбработке();
	Если ПустоеЗначение(ПутьКОбработке)=1 Тогда	
		Сообщить("Указанных файлов не обнаружено.","!");
		УказатьПутьКОбработке();
	Иначе
		КонтФормы=Форма.Параметр;
		ОткрытьФорму("Отчет", КонтФормы, ПутьКОбработке);
	КонецЕсли;
	Форма.Закрыть(0);
КонецПроцедуры // ПриОткрытии